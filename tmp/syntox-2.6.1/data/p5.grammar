@grammar «
#line 1
#file "p5.grammar"

S → NP|cln VP
{
	[subj:S, Rest];
	↓1 := S;
	[number:N, person:P, gender:G] ⊂ ⇓1;
	↓2 := Rest ∪ [number:N, person:P, gender:G];
};

NP → (det) N
{
    order $2 $1 ;
    ↓2 := ↑;
    if ($1) {
       ↓1 := ↑ ∪ ⇓2;
       ⇑ := ↑ ∪ ⇓1 ∪ ⇓2 ∪ [person:three, det:yes];
    } else {
       ⇑ := ↑ ∪ ⇓2 ∪ [person:three, det:no];
    }
};

NP → pn
{
	↓1 := ↑;
	⇑ := ⇓1;
};

NP → constantNoun
{
	↓1 := ↑;
	⇑ := ⇓1;
};

N → N2
{
	[mod:Mod, X];
	Sorted := sort Mod with rank;
	↓ := [X, mod:Sorted];
	⇑ := X ∪ ⇓;
};

N → N2
{
	[mod:NIL, X];
	↓ := X;
	⇑ := ⇓;
};

N2 → A N2
{
	[mod:<Head::Tail>, X];
	[number:N, gender:G] ⊂  ↑;
	↓1 := Head ∪ [number:N, gender:G, pre:yes, post:no];
	[number:N1, gender:G1] ⊂  ⇓1;
	test [pre:yes, post:no] ∪ ⇓1;
	↓2 := [X, mod:Tail] ∪ [number:N1, gender:G1];
	⇑ := X ∪ ⇓2 ∪ [number:N1, gender:G1];
};

N2 → N2 A
{
	[mod:<Head::Tail>, X];
	order $2 $1;
	[number:N, gender:G] ⊂ ↑;
	↓2 := Head ∪ [number:N, gender:G, pre:no, post:yes];
	[number:N2, gender:G2] ⊂ ⇓2;
	test [pre:no, post:yes] ∪ ⇓1;
	↓1 := [X, mod:Tail] ∪ [number:N2, gender:G2];
	⇑ := X ∪ ⇓1 ∪ [number:N2, gender:G2];
};

N2 → nc
{
	[mod:NIL];
	↓1 := ↑;
	⇑ := ⇓1;
};

VP → VP NP
{
	[obj:Obj, Rest];
	[obj:[PRED:P]] ⊂ ↑;
	test P ≠ pro; 
	↓1 := Rest;
	↓2 := Obj ∪ [det:yes];
	⇑ := ⇓1;
};

VP → VP PP
{
	[de_obj:DeObj, Rest];
	[de_obj:[PRED:P]] ⊂ ↑;
	test P ≠ pro; 
	↓1 := Rest;
	↓2 := DeObj ∪ [det:yes, pcas:de];
	⇑ := ⇓1;
};

VP → VP SComp
{
	[scomp:SComp, Rest];
	↓1 := Rest;
	[scomp:SCompSynt] ⊂ ⇓1;
	↓2 := SComp ∪ SCompSynt;
	⇑ := ⇓1;
};

VP → VP VComp
{
	[vcomp:VComp, Rest];
	↓1 := Rest;
	[vcomp:VCompSynt] ⊂ ⇓1;
	↓2 := VComp ∪ VCompSynt;
	⇑ := ⇓1;
};

VP → V (NP)
{
    [obj:NIL, scomp:NIL, de_obj:NIL];
    [objC:Obj, X] ⊂ ↑;
    ↓1 := X;
    [objC:ObjSynt] ⊂ ⇓1;
    if ($2) {
        ↓2 := ObjSynt;
    } else {
	test ¬ObjSynt;
    }
    ⇑ := ⇓1;

};

VP → V
{
	[obj:[PRED:pro], scomp:NIL];
	↓1 := ↑;
	⇑ := ⇓1;
};

V → (clneg) (advneg) CLITICS v (ADVV) (advneg)
{
	order $1 $2 $3 $5 $6 $4; 
	[diathese:active] ⊂ ↑;
	//test (⇓2 ⇔ ⇓1); // bug
	//test (⇓2 ∨ ⇓6) ⇔ ⇓1;
	//test ⇓6; // bug
	//test ¬ (⇓2 ∧ ⇓6); // ok
	test (⇓2 ∧ ⇓6); // ok
	if ($5) {
		[modV:ModV];
		↓5 := [modV:ModV];
	} else {
		[modV:NIL];
	}
	if ($2) {
		[neg:yes];
		//[finite:no];
		↓4 := ↑ ∪ [v_form:tense];
	} 
	else if ($6) {
		[neg:yes];
		//[finite:yes];
		↓4 := ↑ ∪ [v_form:infinitive];
	} else {
		[neg:no] ⊂ ↑;
		↓4 := ↑ ∪ [v_form:tense];
	}
	⇑ := ⇓4;
};

ADVV → adv ADVV
{
	[modV:<Head::Tail>];
        ↓1 := Head;
        ↓2 := [modV:Tail];
};

ADVV →
{
	[modV:NIL];
};

CLITICS → (cld) (cla)
{
	if ($1) {
		[à_obj:[PRED:pro]];
		[à_obj:AObj];
        	↓1 := AObj;
	} else {
		[à_obj:NIL];
	}
	if ($2) {
		[obj:[PRED:pro, person:three]];
		[obj:Obj];
        	↓2 := Obj;
	} else {
		[obj:NIL];
	}
};

CLITICS → cla (cld)
{
	[obj:[PRED:pro, person:one|two]];
	[obj:Obj];
       	↓1 := Obj;
	if ($2) {
		[à_obj:[PRED:pro]];
		[à_obj:AObj];
        	↓2 := AObj;
	} else {
		[à_obj:NIL];
	}
};

SComp → csu T
{
	↓1 := ↑;
       	↓2 := ↑;
	⇑ := ⇓1 ∪ ⇓2;
};

VComp → VP
{
	↓1 := ↑ ∪ [finite:yes];
       	⇑ := ⇓1;
};

PP → prep NP
{
	↓1 := ↑;
       	↓2 := ↑;
	⇑ := ⇓1 ∪ ⇓2;
};
»
